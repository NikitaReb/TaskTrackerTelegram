#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Наименование = СокрЛП(Наименование);
	
	ПроверитьУникальностьТокена(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьУникальностьТокена(Отказ)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Справочник.ПЗТ_ТокеныУправленияТелеграмБотами");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПЗТ_ТокеныУправленияТелеграмБотами.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ПЗТ_ТокеныУправленияТелеграмБотами КАК ПЗТ_ТокеныУправленияТелеграмБотами
	|ГДЕ
	|	ПЗТ_ТокеныУправленияТелеграмБотами.Ссылка <> &Ссылка
	|	И НЕ ПЗТ_ТокеныУправленияТелеграмБотами.ПометкаУдаления
	|	И ПЗТ_ТокеныУправленияТелеграмБотами.Наименование = &Наименование");
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Наименование", Наименование);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Токен доступа %1 уже существует'"), Наименование);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Наименование", "Объект", Отказ);
		
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Иначе
  ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
