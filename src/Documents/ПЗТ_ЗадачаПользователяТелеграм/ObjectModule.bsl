#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Статус) Тогда
		
		Статус = ОбщегоНазначения.ПредопределенныйЭлемент("Перечисление.ПЗТ_СтатусыЗадачиПользователяТелеграм.Создана");							
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Приоритет) Тогда
		
		Приоритет = ОбщегоНазначения.ПредопределенныйЭлемент("Перечисление.ПЗТ_ПриоритетЗадачиТелеграм.Средний");;
		
	КонецЕсли;
 
КонецПроцедуры

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	КраткоеОписаниеЗадачи = ОписаниеЗадачи;
	
	УстановитьПризнакПоставитьЗадачуКОтправкеПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство(СвойствоПоставитьЗадачуКОтправке())
		И ДополнительныеСвойства[СвойствоПоставитьЗадачуКОтправке()]
		И Не ПометкаУдаления Тогда
		
		ПЗТ_ПланировщикЗадачТелеграмСервер.ПоставитьЗадачуВОчередьОтправки(Ссылка);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СвойствоПоставитьЗадачуКОтправке()

	Возврат "ПоставитьЗадачуКОтправке";
	
КонецФункции

Процедура УстановитьПризнакПоставитьЗадачуКОтправкеПередЗаписью()
	
	ИменаРеквизитовПровери = "Статус, Ответ";
	
	НовыеЗначенияРеквизитов = Новый Структура(ИменаРеквизитовПровери);
	ЗаполнитьЗначенияСвойств(НовыеЗначенияРеквизитов, ЭтотОбъект);
	
	СтарыеЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Статус, Ответ");
	
	РеквизитыИзменены =  Не ОбщегоНазначения.КоллекцииИдентичны(НовыеЗначенияРеквизитов, СтарыеЗначенияРеквизитов);
	
	ДополнительныеСвойства.Вставить(СвойствоПоставитьЗадачуКОтправке(), РеквизитыИзменены);

КонецПроцедуры

#КонецОбласти

#Иначе
  ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
