
#Область ПрограммныйИнтерфейс

// Получает все последние сообщения от Телеграм-бота.
// Создает задачи на принятые сообщения.
//
Процедура ПолучитьЗадачиОтПользователейТелеграм() Экспорт

	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ПЗТ_ЗагрузкаЗадачПользователейИзТелеграм);
	
	ДанныеТокена = ДанныеТокенаТелеграм(); 
	
	Если Не ЗначениеЗаполнено(ДанныеТокена.Токен) Тогда
		
		СтрокаОшибки = НСтр("ru = 'Отсутствует токен Telegram-бота. Взаимодействие невозможно.'");
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрацииПодсистемы(), УровеньЖурналаРегистрации.Ошибка, , , СтрокаОшибки);
		ВызватьИсключение(СтрокаОшибки);
		
	КонецЕсли;
	
	ЧастиАдреса = Новый Массив;
	ЧастиАдреса.Добавить(СтрШаблон("%1%2", "bot", ДанныеТокена.Токен));
	ЧастиАдреса.Добавить("getUpdates");
		
	АдресРесурса = СтрСоединить(ЧастиАдреса, "/");
	
	ЗащищенноеСоединение = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
    Сервер = "api.telegram.org";
	ВремяВСекундахПолученияДанных = 180;
	
	Соединение = Новый HTTPСоединение(Сервер, , , , , ВремяВСекундахПолученияДанных,ЗащищенноеСоединение);

	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");

	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	
	Если ЗначениеЗаполнено(ДанныеТокена.НомерПоследнегоПринятогоСообщения) Тогда
		
		ДанныеЗапроса = Новый Структура;
		ДанныеЗапроса.Вставить("offset", Формат(ДанныеТокена.НомерПоследнегоПринятогоСообщения + 1, "ЧДЦ=0; ЧГ=")); 
		
		Запрос.УстановитьТелоИзСтроки(СтрокаJSON(ДанныеЗапроса));
		
	КонецЕсли;
	
	Ответ = Соединение.Получить(Запрос);
	
	ОбработатьОтветПолученияСообщенийТелеграм(Ответ, ДанныеТокена.Токен);
		
КонецПроцедуры

// Отправляет сообщения пользователям с ответами по задачам. 
//
Процедура ОтправитьОтветыПоЗадачамПользователейТелеграм() Экспорт

	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ПЗТ_ЗагрузкаЗадачПользователейИзТелеграм);
	
	НачатьТранзакцию();
	
	Попытка
		
		ДанныеОчереди = ДанныеОчередиЗадачКОтправке();
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.ПЗТ_ЗадачаПользователяТелеграм");
		ЭлементБлокировки.ИсточникДанных = ДанныеОчереди;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "ЗадачаПользователяТелеграм");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		ЗадачиКОтправке = ДанныеОчереди.ВыгрузитьКолонку("ЗадачаПользователяТелеграм");
		
		ДанныеОтветов = ДаныеОтветовПоЗадачам(ЗадачиКОтправке);
		
		Для Каждого Сообщение Из ДанныеОтветов Цикл
			
			Ответ = ОтправитьСообщениеТелеграм(Сообщение);
			
			Если Ответ.КодСостояния <> 200 Тогда
				
				ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось отправить сообщения ответов Telegram-боту. Код состояния %1.'"), Ответ.КодСостояния);
				ВызватьИсключение ТекстОшибки;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ОчередьОтправки = ДанныеОчереди.ВыгрузитьКолонку("ЭлементОчереди");
		
		Для Каждого ЭлементОчереди Из ОчередьОтправки Цикл
			
			ЭлементОчередиОбъект = ЭлементОчереди.ПолучитьОбъект();
			ЭлементОчередиОбъект.Удалить();
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();                              
		
	Исключение
		
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрацииПодсистемы(), 
			УровеньЖурналаРегистрации.Ошибка,
			, 
			, 
			ОбработкаОшибок.ПодробноеПредставлениеОшибки((ИнформацияОбОшибке())));
		
		ВызватьИсключение ОбработкаОшибок.ПодробноеПредставлениеОшибки((ИнформацияОбОшибке()));
		
	КонецПопытки;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Записывает документ для отправки ответа пользователю.
//
// Параметры:
//  ЗадачаПользователя	 - ДокументСсылка.ПЗТ_ЗадачаПользователяТелеграм - 
//
Процедура ПоставитьЗадачуВОчередьОтправки(ЗадачаПользователя) Экспорт
	
	Попытка
		
		ЗадачаКОтправке = Справочники.ПЗТ_ЗадачиКОтправкеПользователю.СоздатьЭлемент();
		ЗадачаКОтправке.Задача = ЗадачаПользователя;
		ЗадачаКОтправке.Записать();
		
	Исключение
			
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрацииПодсистемы(), 
			УровеньЖурналаРегистрации.Ошибка,
			, 
			, 
			ОбработкаОшибок.ПодробноеПредставлениеОшибки((ИнформацияОбОшибке())));
			
		ТекстОшибки = НСтр("ru = 'Не удалось поставить задачу в очередь к отправке'");

		ВызватьИсключение ТекстОшибки;
	
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает данные токена, который установлен как основной в ПЗТ_ТокенУправленияТелеграмБотом.
// 
// Возвращаемое значение:
//   Структура:
//		* Токен 							- Строка - Токен доступа 
//   	* НомерПоследнегоПринятогоСообщения - Число - Номер последнего сообщения, что принимать только новые.
//
Функция ДанныеТокенаТелеграм()
	
	Результат = Новый Структура;
	Результат.Вставить("Токен", "");
	Результат.Вставить("НомерПоследнегоПринятогоСообщения", 0);
	
	ОсновнойТокен = Константы.ПЗТ_ТокенУправленияТелеграмБотом.Получить();
	
	ДанныеОсновногоТокена = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОсновнойТокен, "Наименование, НомерПоследнегоПринятогоСообщения");
	ЗаполнитьЗначенияСвойств(Результат, ДанныеОсновногоТокена);
	
	Результат.Токен = ДанныеОсновногоТокена.Наименование;

	Возврат Результат;
		
КонецФункции

// Возвращает имя события для записи в журнал регистрации
// 
// Возвращаемое значение:
//   Строка - Имя события.  
//
Функция СобытиеЖурналаРегистрацииПодсистемы()
	
	Возврат НСтр("ru = 'Взаимодействие с Telegram-ботом'");
	
КонецФункции

Функция СтрокаJSON(ОбъектJSON)
	
	ПараметрыЗаписи = Новый ПараметрыЗаписиJSON(, Символы.Таб);
	
	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку(ПараметрыЗаписи);
	ЗаписатьJSON(Запись, ОбъектJSON);
	
	Возврат Запись.Закрыть();
	
КонецФункции

#Область СлужебныеПроцедурыИФункции_ПолучениеДанных

// Возвращает данные входящих сообщений преобразованных из JSON
//
// Параметры:
//  СтрокаJSON	 - Строка - Сообщения из телеграм.
// 
// Возвращаемое значение:
//   Массив из Структура - см.НовыеДанныеВходящегоСообщенияТелеграм. 
//
Функция ДанныеЗадачИзСообщенийТелеграм(СтрокаJSON)
	
	Результат = Новый Массив;
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(СтрокаJSON);
	ДанныеВходящихСообщений = ПрочитатьJSON(ЧтениеОтвета);
	
	Для Каждого Сообщение Из ДанныеВходящихСообщений.result Цикл
		
		Если Не Сообщение.Свойство("message") Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не Сообщение.message.Свойство("text") Тогда
			Продолжить;
		КонецЕсли; 
		
		ЭтоКомандаБоту = Лев(Сообщение.message.text, 1) = "/";
		
		Если ЭтоКомандаБоту Тогда
			Продолжить;
		КонецЕсли;
		
		ЗадачаПользователя = НовыеДанныеЗадачиПользователя();
		
		ЗадачаПользователя.НомерСообщения = Сообщение.update_id;
		ЗадачаПользователя.ИдентификаторЧата = Сообщение.message.chat.id;
		ЗадачаПользователя.ОписаниеЗадачи = Сообщение.message.text;
		
		Если Сообщение.message.chat.Свойство("username") Тогда
			ЗадачаПользователя.ПубличноеИмяПользователя = Сообщение.message.chat.username;
		КонецЕсли;
		
		ИмяФамилияПользователя = Новый Массив;
		
		Если Сообщение.message.chat.Свойство("first_name") Тогда
			ИмяФамилияПользователя.Добавить(Сообщение.message.chat.first_name);
		КонецЕсли;
		
		Если Сообщение.message.chat.Свойство("last_name") Тогда
			ИмяФамилияПользователя.Добавить(Сообщение.message.chat.last_name);
		КонецЕсли;
		
		ЗадачаПользователя.ИмяФамилияПользователя = СтрСоединить(ИмяФамилияПользователя, " ");
		
		Результат.Добавить(ЗадачаПользователя);
		
	КонецЦикла;
			
	Возврат Результат;
	
КонецФункции

// Записывает номер последнего сообщения для токена, который установлен в ПЗТ_ТокенУправленияТелеграмБотом.
//
// Параметры:
//  НомерСообщения	 - Число -  
//
Процедура ЗафиксироватьНомерПоследнегоСообщенияОсновногоТокена(НомерСообщения)
	
	Если Не ЗначениеЗаполнено(НомерСообщения) Тогда
		Возврат;
	КонецЕсли;
	
	ОсновнойТокен = Константы.ПЗТ_ТокенУправленияТелеграмБотом.Получить();
	
	ОсновнойТокенОбъект = ОсновнойТокен.ПолучитьОбъект();
	ОсновнойТокенОбъект.НомерПоследнегоПринятогоСообщения = НомерСообщения;
	ОсновнойТокенОбъект.Записать();
	
КонецПроцедуры

// Конструктор данных задач из телеграм.
// 
// Возвращаемое значение:
//   Структура:
//		* НомерСообщения 			- Число - 
//   	* ИдентификаторЧата 		- Число -
//	 	* ПубличноеИмяПользователя	- Строка -
//	 	* ИмяФамилияПользователя	- Строка -
//	 	* ОписаниеЗадачи  			- Строка -
//
Функция НовыеДанныеЗадачиПользователя()
	
	Результат = Новый Структура;
	Результат.Вставить("НомерСообщения", 0);
	Результат.Вставить("ИдентификаторЧата", 0);
	Результат.Вставить("ПубличноеИмяПользователя", "");
	Результат.Вставить("ИмяФамилияПользователя", "");
	Результат.Вставить("ОписаниеЗадачи", "");
	
	Возврат Результат;
	
КонецФункции

// Обрабатывает ответ от Телеграм-бота.
//  В транзакции создает документы задач на каждое сообщение.
//
// Параметры:
//  Ответ	 - HTTPОтвет	 - Ответ от Телеграм-бота.
//  Токен	 - СправочникСсылка	 - Токен Телеграм-бота.
//
Процедура ОбработатьОтветПолученияСообщенийТелеграм(Ответ, Токен)
	
	НачатьТранзакцию();
	
	Попытка
		
		Если Ответ.КодСостояния = 200 Тогда
			
			ТелоЗапроса = Ответ.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
			ДанныеЗадач = ДанныеЗадачИзСообщенийТелеграм(ТелоЗапроса);
			
			НомерПоследнегоСообщения = 0;
			
			Для Каждого Задача Из ДанныеЗадач Цикл
				
				ДокументЗадачи = Документы.ПЗТ_ЗадачаПользователяТелеграм.СоздатьДокумент();
				ДокументЗадачи.Дата = ТекущаяДатаСеанса();
				ДокументЗадачи.ТокенБота = Токен;
				ДокументЗадачи.Заполнить(Задача);
				ДокументЗадачи.Записать(РежимЗаписиДокумента.Проведение);
				
				НомерПоследнегоСообщения = Задача.НомерСообщения;
				
			КонецЦикла;
			
			ЗафиксироватьНомерПоследнегоСообщенияОсновногоТокена(НомерПоследнегоСообщения);
			
		Иначе
			
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось получить задачи Telegram-бота. Код состояния %1.'"), Ответ.КодСостояния);
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();                              
		
	Исключение
		
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрацииПодсистемы(), 
			УровеньЖурналаРегистрации.Ошибка,
			, 
			, 
			ОбработкаОшибок.ПодробноеПредставлениеОшибки((ИнформацияОбОшибке())));
		
		ВызватьИсключение ОбработкаОшибок.ПодробноеПредставлениеОшибки((ИнформацияОбОшибке()));
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_ОтправкаДанных

// Возвращает данные для формирования сообщений ответов по задачам.
//
// Параметры:
//  ЗадачиПользователей	 - Массив из ДокументСсылка.ПЗТ_ЗадачаПользователяТелеграм - 
// 
// Возвращаемое значение:
//  Массив из Структура - ключи структуры см.НовыеДанныеОтветовПоЗадачам.
//
Функция ДаныеОтветовПоЗадачам(ЗадачиПользователей)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПЗТ_ЗадачаПользователяТелеграм.Ссылка КАК Задача,
	|	ПЗТ_ЗадачаПользователяТелеграм.ТокенБота КАК Токен,
	|	ПЗТ_ЗадачаПользователяТелеграм.ИдентификаторЧата КАК ИдентификаторЧата,
	|	ПЗТ_ЗадачаПользователяТелеграм.Статус КАК Статус,
	|	ПЗТ_ЗадачаПользователяТелеграм.ОписаниеЗадачи КАК ОписаниеЗадачи,
	|	ПЗТ_ЗадачаПользователяТелеграм.Ответ КАК Ответ
	|ИЗ
	|	Документ.ПЗТ_ЗадачаПользователяТелеграм КАК ПЗТ_ЗадачаПользователяТелеграм
	|ГДЕ
	|	ПЗТ_ЗадачаПользователяТелеграм.Ссылка В (&ЗадачиПользователей)");
	
	Запрос.УстановитьПараметр("ЗадачиПользователей", ЗадачиПользователей);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Результат = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеОтвета = НовыеДанныеОтветовПоЗадачам();
		ЗаполнитьЗначенияСвойств(ДанныеОтвета, Выборка);
		
		ЧастиОтвета = Новый Массив;
		ЧастиОтвета.Добавить(СтрШаблон(НСтр("ru = '<b>%1</b>'"), Строка(Выборка.Задача)));
		ЧастиОтвета.Добавить(СтрШаблон(НСтр("ru = '<b>Статус:</b> %1'"), Выборка.Статус));
		ЧастиОтвета.Добавить(СтрШаблон(НСтр("ru = '<b>Описание:</b> %1'"), Выборка.ОписаниеЗадачи));
		ЧастиОтвета.Добавить(СтрШаблон(НСтр("ru = '<b>Ответ:</b> %1'"), Выборка.Ответ));
		
		ДанныеОтвета.ТекстСообщения = СтрСоединить(ЧастиОтвета, Символы.ПС);
		
		Результат.Добавить(ДанныеОтвета);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает задачи в очереди, по которым нужно сформировать ответ.
// 
// Возвращаемое значение:
//   ТаблицаЗначений - Колонки:
// 		* ЗадачаПользователяТелеграм - ДокументСсылка.ПЗТ_ЗадачаПользователяТелеграм - 
//      * ЭлементОчереди 			 - СправочникСслыка.ПЗТ_ЗадачиКОтправкеПользователю - 
//
Функция ДанныеОчередиЗадачКОтправке()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПЗТ_ЗадачиКОтправкеПользователю.Задача КАК ЗадачаПользователяТелеграм,
	|	ПЗТ_ЗадачиКОтправкеПользователю.Ссылка КАК ЭлементОчереди
	|ИЗ
	|	Справочник.ПЗТ_ЗадачиКОтправкеПользователю КАК ПЗТ_ЗадачиКОтправкеПользователю");
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Конструктор данных ответа на задачи Телеграм.
// 
// Возвращаемое значение:
//   Структура:
//		* Токен 			- Строка - 
//   	* ИдентификаторЧата - Число -
//	 	* ТекстСообщения	- Строка -
//
Функция НовыеДанныеОтветовПоЗадачам()
	
	Результат = Новый Структура;
	Результат.Вставить("Токен", "");
	Результат.Вставить("ИдентификаторЧата", 0);
	Результат.Вставить("ТекстСообщения", "");
	
	Возврат Результат;
	
КонецФункции

// Отправляет сообщения телеграм боту
//
// Параметры:
//  ДанныеСообщения	 - Структура - см.НовыеДанныеОтветовПоЗадачам.
// 
// Возвращаемое значение:
//   HTTPОтвет - Ответ от Телеграм-бота.  
//
Функция ОтправитьСообщениеТелеграм(ДанныеСообщения)
		
	ЧастиАдреса = Новый Массив;
	ЧастиАдреса.Добавить(СтрШаблон("%1%2", "bot", ДанныеСообщения.Токен));
	ЧастиАдреса.Добавить("sendMessage");
		
	АдресРесурса = СтрСоединить(ЧастиАдреса, "/");
	
	ЗащищенноеСоединение = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
    Сервер = "api.telegram.org";
	ВремяВСекундахПередачаДанных = 180;
	
	Соединение = Новый HTTPСоединение(Сервер, , , , , ВремяВСекундахПередачаДанных,ЗащищенноеСоединение);

	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	
	ДанныеЗапроса = Новый Структура;
	ДанныеЗапроса.Вставить("chat_id", ДанныеСообщения.ИдентификаторЧата);
	ДанныеЗапроса.Вставить("text", ДанныеСообщения.ТекстСообщения);
	ДанныеЗапроса.Вставить("parse_mode", "HTML");

	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	Запрос.УстановитьТелоИзСтроки(СтрокаJSON(ДанныеЗапроса));
	
	Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	Возврат Ответ;
	
КонецФункции

#КонецОбласти

#КонецОбласти