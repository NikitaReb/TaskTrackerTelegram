
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗагружатьЗадачи = РегламентныеЗаданияСервер.РегламентноеЗаданиеИспользуется(Метаданные.РегламентныеЗадания.ПЗТ_ЗагрузкаЗадачПользователейИзТелеграм);
	ОтправлятьОтветыПоЗадачам = РегламентныеЗаданияСервер.РегламентноеЗаданиеИспользуется(Метаданные.РегламентныеЗадания.ПЗТ_ОтправкаОтветовПоЗадачамПользователейТелеграм);
	
	УстановитьВидимостьКомандОткрытияРасписанияРегламентныхЗаданий(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьИнтерфейсПрограммы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТокенУправленияТелеграмБотомПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);

КонецПроцедуры

&НаКлиенте
Процедура ЗагружатьЗадачиПриИзменении(Элемент)
	
	УстановитьИспользованиеРегламентногоЗадания("ПЗТ_ЗагрузкаЗадачПользователейИзТелеграм", ЗагружатьЗадачи);
	УстановитьВидимостьКомандОткрытияРасписанияРегламентныхЗаданий(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОтправлятьОтветыПоЗадачамПриИзменении(Элемент)
	
	УстановитьИспользованиеРегламентногоЗадания("ПЗТ_ОтправкаОтветовПоЗадачамПользователейТелеграм", ОтправлятьОтветыПоЗадачам);
	УстановитьВидимостьКомандОткрытияРасписанияРегламентныхЗаданий(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьРасписаниеЗагрузкиЗадач(Команда)

	ОткрытьРасписаниеРегЗадания("ПЗТ_ЗагрузкаЗадачПользователейИзТелеграм");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРасписаниеОтправкиОтветовПоЗадачам(Команда)
	
	ОткрытьРасписаниеРегЗадания("ПЗТ_ОтправкаОтветовПоЗадачамПользователейТелеграм");

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ПриИзмененииРеквизита(Элемент, ОбновлятьИнтерфейс = Истина)
	
	ИмяКонстанты = ПриИзмененииРеквизитаСервер(Элемент.Имя);
	ОбновитьПовторноИспользуемыеЗначения();
	
	Если ОбновлятьИнтерфейс Тогда
		ОбновитьИнтерфейс = Истина;
		ПодключитьОбработчикОжидания("ОбновитьИнтерфейсПрограммы", 2, Истина);
	КонецЕсли;
	
	Если ИмяКонстанты <> "" Тогда
		Оповестить("Запись_НаборКонстант", Новый Структура, ИмяКонстанты);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнтерфейсПрограммы()
	
	Если ОбновитьИнтерфейс = Истина Тогда
		ОбновитьИнтерфейс = Ложь;
		ОбщегоНазначенияКлиент.ОбновитьИнтерфейсПрограммы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПриИзмененииРеквизитаСервер(ИмяЭлемента)
	
	РеквизитПутьКДанным = Элементы[ИмяЭлемента].ПутьКДанным;
	ИмяКонстанты = СохранитьЗначениеРеквизита(РеквизитПутьКДанным);
	ОбновитьПовторноИспользуемыеЗначения();
	Возврат ИмяКонстанты;
	
КонецФункции

&НаСервере
Функция СохранитьЗначениеРеквизита(РеквизитПутьКДанным)
	
	ЧастиИмени = СтрРазделить(РеквизитПутьКДанным, ".");
	Если ЧастиИмени.Количество() <> 2 Тогда
		Возврат "";
	КонецЕсли;
	
	ИмяКонстанты = ЧастиИмени[1];
	КонстантаМенеджер = Константы[ИмяКонстанты];
	КонстантаЗначение = НаборКонстант[ИмяКонстанты];
	
	Если КонстантаМенеджер.Получить() <> КонстантаЗначение Тогда
		КонстантаМенеджер.Установить(КонстантаЗначение);
	КонецЕсли;
	
	Возврат ИмяКонстанты;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьРасписаниеРегЗадания(ИмяРегЗадания);
	
	Идентификатор = ИндентификаторРегЗадания(ИмяРегЗадания);
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(
		ПолучитьРасписание(Идентификатор));
	
	Диалог.Показать(Новый ОписаниеОповещения(
		"ОткрытьРасписаниеЗавершение", ЭтотОбъект, Идентификатор));
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРасписаниеЗавершение(НовоеРасписание, Идентификатор) Экспорт

	Если НовоеРасписание <> Неопределено Тогда		
		УстановитьРасписание(Идентификатор, НовоеРасписание);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИндентификаторРегЗадания(ИмяРегЗадания)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат РегламентныеЗаданияСервер.УникальныйИдентификатор(
		Метаданные.РегламентныеЗадания[ИмяРегЗадания]);
		
	УстановитьПривилегированныйРежим(Ложь);

КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьИспользованиеРегламентногоЗадания(ИмяРегЗадания, Использование)
	
	УстановитьПривилегированныйРежим(Истина);
	
	РегламентныеЗаданияСервер.УстановитьИспользованиеРегламентногоЗадания(
		ИндентификаторРегЗадания(ИмяРегЗадания), 
		Использование);
		
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры
	
&НаСервереБезКонтекста
Функция ПолучитьРасписание(Знач ИдентификаторРегламентногоЗадания)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат РегламентныеЗаданияСервер.РасписаниеРегламентногоЗадания(
		ИдентификаторРегламентногоЗадания);
		
	УстановитьПривилегированныйРежим(Ложь);
	
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьРасписание(Знач ИдентификаторРегламентногоЗадания, Знач Расписание)
	
	УстановитьПривилегированныйРежим(Истина);
	
	РегламентныеЗаданияСервер.УстановитьРасписаниеРегламентногоЗадания(
		ИдентификаторРегламентногоЗадания,
		Расписание);
		
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьКомандОткрытияРасписанияРегламентныхЗаданий(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ОткрытьРасписаниеЗагрузкиЗадач.Видимость = Форма.ЗагружатьЗадачи;
	Элементы.ОткрытьРасписаниеОтправкиОтветовПоЗадачам.Видимость = Форма.ОтправлятьОтветыПоЗадачам;

КонецПроцедуры

#КонецОбласти
